!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
!     ###############################################################################
SUBROUTINE ASSIM_EXTRAPOLATE_FIELD(HPROGRAM,PLON_IN,PLAT_IN,PFIELD_IN,OLKEEPEXTZONE,LD_MASKEXT,PFIELD_OUT,PALT,PLSM)
USE YOMHOOK,         ONLY : LHOOK, DR_HOOK, JPHOOK
USE PARKIND1,        ONLY : JPIM
USE MODD_SURF_ATM_n, ONLY : SURF_ATM_t
USE MODD_SURF_PAR,   ONLY : XUNDEF
USE MODD_ASSIM,      ONLY : NPRINTLEV,LPIO
USE MODD_SURFEX_MPI, ONLY : NINDEX
USE MODI_ASSIM_GATHER_FIELD
USE MODI_ASSIM_DIST_FIELD
USE MODI_GET_LUOUT
USE MODI_ABOR1_SFX
USE MODD_SURFEX_HOST

IMPLICIT NONE
CHARACTER(LEN=6),    INTENT(IN)    :: HPROGRAM
REAL,DIMENSION(:),   INTENT(IN)    :: PLON_IN
REAL,DIMENSION(:),   INTENT(IN)    :: PLAT_IN
REAL,DIMENSION(:),   INTENT(IN)    :: PFIELD_IN
LOGICAL,             INTENT(IN)    :: OLKEEPEXTZONE
LOGICAL,DIMENSION(:),INTENT(IN)    :: LD_MASKEXT
REAL,DIMENSION(:),   INTENT(OUT)   :: PFIELD_OUT
REAL,DIMENSION(:),   INTENT(IN),OPTIONAL :: PALT
REAL,DIMENSION(:),   INTENT(IN),OPTIONAL :: PLSM

REAL,DIMENSION(:),ALLOCATABLE :: ZFIELD,ZFIELD0,ZLON_IN,ZLAT_IN,ZLSM
REAL,DIMENSION(:),ALLOCATABLE :: ZALT,ZLON0,ZLAT0,ZFIELD_EP,ZALT0,ZLSM0
LOGICAL,ALLOCATABLE,DIMENSION(:) :: GINTERP,OD_MASKEXT
INTEGER,ALLOCATABLE,DIMENSION(:) :: KMASK 
INTEGER                          :: JI,JJ,ISIZE,ISIZE0,ILUOUT

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('ASSIM_EXTRAPOLATE_FIELD',0,ZHOOK_HANDLE)

CALL GET_LUOUT(HPROGRAM,ILUOUT)
! Get global size
IF(HPROGRAM == 'AROME ') THEN
  ISIZE = YRSURFEX_HOST%ASSIM_GET_SIZE_GLOBAL (PFIELD_IN)
ELSE
  ISIZE=SIZE(NINDEX)
ENDIF

IF ( LPIO ) THEN
  ALLOCATE(ZFIELD(ISIZE))
  ALLOCATE(ZLON_IN(ISIZE))
  ALLOCATE(ZLAT_IN(ISIZE))
  ALLOCATE(OD_MASKEXT(ISIZE))
  IF ( PRESENT(PALT)) ALLOCATE(ZALT(ISIZE))
  IF ( PRESENT(PLSM)) ALLOCATE(ZLSM(ISIZE))
ELSE
  ALLOCATE(ZFIELD(0))
  ALLOCATE(ZLON_IN(0))
  ALLOCATE(ZLAT_IN(0))
  ALLOCATE(OD_MASKEXT(0))
  IF ( PRESENT(PALT)) ALLOCATE(ZALT(0))
  IF ( PRESENT(PLSM)) ALLOCATE(ZLSM(0))
ENDIF

! Gather fields from processors
CALL ASSIM_GATHER_FIELD(HPROGRAM,PLON_IN,ZLON_IN)
CALL ASSIM_GATHER_FIELD(HPROGRAM,PLAT_IN,ZLAT_IN)
CALL ASSIM_GATHER_FIELD(HPROGRAM,PFIELD_IN,ZFIELD)

!TODO: Communicate logicals
!CALL ASSIM_GATHER_FIELD(HPROGRAM,LD_MASKEXT,OD_MASKEXT)
IF ( PRESENT(PALT)) CALL ASSIM_GATHER_FIELD(HPROGRAM,PALT,ZALT)
IF ( PRESENT(PLSM)) CALL ASSIM_GATHER_FIELD(HPROGRAM,PLSM,ZLSM)

! Do extrapolations on IO processor
IF ( LPIO ) THEN
  
  ALLOCATE(KMASK(ISIZE))
  KMASK=0

  ISIZE0=COUNT(ZLON_IN /= XUNDEF)
  ALLOCATE(ZLON0(ISIZE0))
  ALLOCATE(ZLAT0(ISIZE0))
  ALLOCATE(ZFIELD0(ISIZE0))
  ALLOCATE(ZFIELD_EP(ISIZE0))
  ALLOCATE(GINTERP(ISIZE0))
  
  IF ( PRESENT(PALT) ) ALLOCATE(ZALT0(ISIZE0))
  IF ( PRESENT(PLSM) ) ALLOCATE(ZLSM0(ISIZE0))
  GINTERP(:) = .FALSE.

  JJ=1  
  DO JI=1,ISIZE
    ! Work on defined values
    IF ( ZLON_IN(JI) /= XUNDEF ) THEN
      ! Always do or only if not in extension zone
!      IF ( (OLKEEPEXTZONE) .OR. .NOT.OD_MASKEXT(JI)) THEN
        KMASK(JJ)=JI
        ZLON0(JJ)=ZLON_IN(JI)
        ZLAT0(JJ)=ZLAT_IN(JI)
        ZFIELD0(JJ)=ZFIELD(JI)
        IF ( PRESENT(PALT) ) ZALT0(JJ)=ZALT(JI)
!        IF (OLKEEPEXTZONE .AND. OD_MASKEXT(JI) ) ZFIELD0(JJ)=XUNDEF
        IF ( ZFIELD0(JJ) == XUNDEF ) GINTERP(JJ) = .TRUE.
        IF ( PRESENT(PLSM) ) THEN
          ZLSM0(JJ)=ZLSM(JI)
          IF ( ZLSM0(JJ) < 0.5 ) THEN
            ZFIELD0(JJ) = XUNDEF
            GINTERP(JJ) = .TRUE.           
          ENDIF
        ENDIF
        JJ=JJ+1
!      ENDIf
    ENDIF
  ENDDO

  ZFIELD_EP(:) = ZFIELD0(:)
  IF ( COUNT(ZFIELD0 == XUNDEF ) == 0 ) THEN
    WRITE(*,*) 'No points to extrapolate'
  ELSE
    IF ( PRESENT(PALT) ) THEN
      CALL YRSURFEX_HOST%OI_HOR_EXTRAPOL_SURF(ISIZE0,ZLAT0,ZLON0,ZFIELD0,ZLAT0,ZLON0,ZFIELD_EP,GINTERP,ZALT0)
    ELSE
      CALL YRSURFEX_HOST%OI_HOR_EXTRAPOL_SURF(ISIZE0,ZLAT0,ZLON0,ZFIELD0,ZLAT0,ZLON0,ZFIELD_EP,GINTERP)
    ENDIF
  ENDIF
  WRITE(ILUOUT,*) 'Extrapolated ',COUNT(ZFIELD0 == XUNDEF ),' values'
  !*     Print values produced by OI_HO_EXTRAPOL_SURF
  IF ( NPRINTLEV > 2 ) THEN
    DO JI=1,ISIZE0
      IF (GINTERP(JI)) THEN
        WRITE(ILUOUT,*) 'Value set to ',ZFIELD_EP(JI),'from nearest neighbour'
      ENDIF
    ENDDO
  ENDIF

  ! Copy values back to mask
  ZFIELD(:)=XUNDEF
  DO JI=1,ISIZE0
    ZFIELD(KMASK(JI))=ZFIELD_EP(JI)
  ENDDO
  DEALLOCATE(KMASK)
  DEALLOCATE(ZFIELD0)
  DEALLOCATE(ZFIELD_EP)
  IF ( PRESENT (PALT)) DEALLOCATE(ZALT0)
  IF ( PRESENT (PLSM)) DEALLOCATE(ZLSM0)

ENDIF

! Distribute global values back to each processor
CALL YRSURFEX_HOST%ASSIM_DIST_FIELD(ZFIELD,PFIELD_OUT)

DEALLOCATE(ZFIELD)
DEALLOCATE(ZLON_IN)
DEALLOCATE(ZLAT_IN)
DEALLOCATE(OD_MASKEXT)
IF ( PRESENT(PALT)) DEALLOCATE(ZALT)
IF ( PRESENT(PLSM)) DEALLOCATE(ZLSM)
IF (LHOOK) CALL DR_HOOK('ASSIM_EXTRAPOLATE_FIELD',1,ZHOOK_HANDLE)
END SUBROUTINE ASSIM_EXTRAPOLATE_FIELD
