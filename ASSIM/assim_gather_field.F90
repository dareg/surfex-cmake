!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
SUBROUTINE ASSIM_GATHER_FIELD(HPROGRAM,PFIELD_LOCAL,PFIELD_GLOBAL)
  USE PARKIND1,        ONLY : JPIM
  USE YOMHOOK,         ONLY : LHOOK, DR_HOOK, JPHOOK
  USE MODD_ASSIM,      ONLY : LPIO
  USE MODD_SURFEX_MPI, ONLY : NPROC,NINDEX
  USE MODD_SURF_PAR,   ONLY : XUNDEF
  USE MODI_GATHER_AND_WRITE_MPI
  USE MODD_SURFEX_HOST
  USE MODI_ABOR1_SFX

  IMPLICIT NONE
  CHARACTER(LEN=6),  INTENT(IN)  :: HPROGRAM
  REAL,DIMENSION(:), INTENT(IN)  :: PFIELD_LOCAL
  REAL,DIMENSION(:), INTENT(OUT) :: PFIELD_GLOBAL
  INTEGER                        :: ISIZE,JI
  REAL,DIMENSION(:),ALLOCATABLE  :: ZWORK

  REAL(KIND=JPHOOK)                :: ZHOOK_HANDLE
  IF (LHOOK) CALL DR_HOOK('ASSIM_GATHER_FIELD',0,ZHOOK_HANDLE)

  IF(HPROGRAM == 'AROME ') THEN
    ISIZE = YRSURFEX_HOST%ASSIM_GET_SIZE_GLOBAL(PFIELD_LOCAL)
    IF (LPIO) THEN
      ALLOCATE(ZWORK(ISIZE))
      ZWORK(:)=XUNDEF
    ELSE
      ! This breaks bounds checking
      ALLOCATE(ZWORK(0))
    ENDIF
    CALL YRSURFEX_HOST%ASSIM_GATHER_FIELD (PFIELD_LOCAL,ZWORK(:))
  ELSE
    ISIZE=SIZE(NINDEX)
    IF (LPIO) THEN
      IF ( SIZE(PFIELD_GLOBAL) /= ISIZE ) THEN
        WRITE(*,*) "SIZE(PFIELD_GLOBAL):",SIZE(PFIELD_GLOBAL),"ISIZE:",ISIZE
        CALL ABOR1_SFX('Mismatch in dimensions between global fields')
      ENDIF

      ALLOCATE(ZWORK(ISIZE))
      ZWORK(:)=XUNDEF
    ELSE
      ALLOCATE(ZWORK(0))
    ENDIF

    ! Gather field
    IF ( NPROC > 1 ) THEN
       CALL GATHER_AND_WRITE_MPI(PFIELD_LOCAL(:),ZWORK(:))
    ELSE
       ZWORK(1:SIZE(PFIELD_LOCAL))=PFIELD_LOCAL(:)
    ENDIF
  ENDIF

  IF (LPIO) THEN
    PFIELD_GLOBAL(:)=ZWORK(:)
  ENDIF
  DEALLOCATE(ZWORK)
  IF (LHOOK) CALL DR_HOOK('ASSIM_GATHER_FIELD',1,ZHOOK_HANDLE)
END SUBROUTINE ASSIM_GATHER_FIELD
