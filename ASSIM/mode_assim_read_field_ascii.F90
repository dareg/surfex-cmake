!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
! *****************************************************************************************

MODULE MODE_ASSIM_READ_FIELD_ASCII

INTERFACE ASSIM_READ_FIELD_ASCII
  MODULE PROCEDURE ASSIM_READ_FIELD_ASCII
END INTERFACE

CONTAINS

SUBROUTINE ASSIM_READ_FIELD_ASCII(U, UG, TPTIME, HMASK, HREC, PFIELD)

USE MODD_SURF_ATM_n,      ONLY : SURF_ATM_t
USE MODD_SURF_ATM_GRID_n, ONLY : SURF_ATM_GRID_t
USE MODD_TYPE_DATE_SURF,  ONLY : DATE_TIME
USE MODD_ASSIM,           ONLY : LOBSHEADER,LOBSNAT,COBS,NPRINTLEV,NOBSTYPE,LPIO
USE MODD_SURFEX_MPI,      ONLY : NPROC
USE MODD_SURF_PAR,        ONLY : XUNDEF
USE MODE_EKF,             ONLY : GET_FILE_NAME
USE YOMHOOK,              ONLY : LHOOK,DR_HOOK, JPHOOK

USE MODI_GET_LUOUT
USE MODI_READ_AND_SEND_MPI
USE MODI_ABOR1_SFX

IMPLICIT NONE
!
!*      0.1    declarations of arguments

TYPE(SURF_ATM_t),      INTENT(INOUT) :: U
TYPE(SURF_ATM_GRID_t), INTENT(INOUT) :: UG
TYPE(DATE_TIME),       INTENT(IN)    :: TPTIME
CHARACTER(LEN=6),      INTENT(IN)    :: HMASK
CHARACTER(LEN=*),      INTENT(IN)    :: HREC
REAL,DIMENSION(:),     INTENT(OUT)   :: PFIELD
CHARACTER(LEN=200)                   :: YFILENAME
INTEGER, PARAMETER                   :: NFG=9
INTEGER, PARAMETER                   :: NCLIM=2
INTEGER, PARAMETER                   :: NSST=2
CHARACTER(LEN=28),DIMENSION(NFG)     :: YFG
CHARACTER(LEN=28),DIMENSION(NCLIM)   :: YCLIM
CHARACTER(LEN=28),DIMENSION(NSST)    :: YSST
CHARACTER(LEN=28),DIMENSION(:),ALLOCATABLE :: YOBSINFILE
LOGICAL :: LOBS
INTEGER :: IID,JI,IYEAR,IMONTH,IDAY,IHOUR,ISTAT,INVAR,INI,IOBS,JJ,ILUOUT
REAL    :: ZTIME
REAL,DIMENSION(:),ALLOCATABLE  :: ZWORK_FULL
REAL,DIMENSION(:),ALLOCATABLE  :: ZWORK_TMP
REAL,DIMENSION(:),ALLOCATABLE  :: ZWORK
REAL,DIMENSION(:),ALLOCATABLE  :: ZLINE
REAL(KIND=JPHOOK)      :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('ASSIM_READ_FIELD_ASCII',0,ZHOOK_HANDLE)

! Let's allocate some arrays
ALLOCATE(ZWORK(U%NSIZE_FULL))
ALLOCATE(ZWORK_FULL(U%NDIM_FULL))

! Do reading for NPIO
IF (LPIO) THEN

  ! Setup for reading
  INI=U%NDIM_FULL
  LOBS=.FALSE.
  IYEAR  = TPTIME%TDATE%YEAR
  IMONTH = TPTIME%TDATE%MONTH
  IDAY   = TPTIME%TDATE%DAY
  ZTIME  = TPTIME%TIME
  IHOUR  = NINT(ZTIME/3600.)
  IID=-1
  ! Check if this is a first guess field
  YFG(1)="CON_RAIN"
  YFG(2)="STRAT_RAIN"
  YFG(3)="CON_SNOW"
  YFG(4)="STRAT_SNOW"
  YFG(5)="CLOUDS"
  YFG(6)="EVAP"
  YFG(7)="EVAPTR"
  YFG(8)="U10M"
  YFG(9)="V10M"
  DO JI=1,NFG
    IF ( TRIM(YFG(JI)) == TRIM(HREC)) THEN
      ! First Guess variables for OI
      YFILENAME='FIRST_GUESS_'
      CALL GET_FILE_NAME(IYEAR,IMONTH,IDAY,IHOUR,YFILENAME)
      YFILENAME=TRIM(YFILENAME)//".DAT"
      INVAR=NFG
      IID=JI
    ENDIF
  ENDDO
  ! Check if this is a climate field
  YCLIM(1)="SWEC"
  YCLIM(2)="TSC"
  DO JI=1,NCLIM
    IF ( TRIM(YCLIM(JI)) == TRIM(HREC)) THEN
      YFILENAME="CLIMATE.DAT"
      INVAR=NCLIM
      IID=JI
    ENDIF
  ENDDO
  ! Check if this is a SST/SIC field
  YSST(1)="SST"
  YSST(2)="SIC"
  DO JI=1,NSST
    IF ( TRIM(YSST(JI)) == TRIM(HREC)) THEN
      YFILENAME="SST_SIC.DAT"
      INVAR=NSST
      IID=JI
    ENDIF
  ENDDO
  IF ( TRIM(HREC) == "LSM" ) THEN
    YFILENAME="LSM.DAT"
    INVAR=1
    IID=1
  ENDIF
  ! Check if this is a observation
  DO JI = 1,NOBSTYPE
    IF ( TRIM(COBS(JI)) == TRIM(HREC)) THEN
      LOBS=.TRUE.
      YFILENAME='OBSERVATIONS_'
      CALL GET_FILE_NAME(IYEAR,IMONTH,IDAY,IHOUR,YFILENAME)
      YFILENAME=TRIM(YFILENAME)//".DAT"
      INVAR=NOBSTYPE
      IID=JI
      IF ( LOBSNAT ) INI=U%NDIM_NATURE
    ENDIF
  ENDDO

  ! Default case if not found above
  IF ( IID < 0 ) THEN
    YFILENAME=TRIM(HREC)//".DAT"
    CALL GET_LUOUT('ASCII ',ILUOUT)
    WRITE(ILUOUT,*) "WARNING: "//TRIM(HREC)//" was not defined!" 
    WRITE(ILUOUT,*) "Assume it to be a single variable ascii file called "//TRIM(YFILENAME)
    INVAR=1
    IID=1
  ENDIF

  ISTAT = 0
  OPEN(UNIT=55,FILE=TRIM(YFILENAME),FORM='FORMATTED',STATUS='OLD',IOSTAT=ISTAT)
  IF ( ISTAT /= 0 ) CALL ABOR1_SFX("Can not open "//TRIM(YFILENAME))

  IF ( LOBS .AND. LOBSHEADER ) THEN
    ! Read in first line and check if variables are consistent
    ALLOCATE(YOBSINFILE(NOBSTYPE))
    READ (55,*,IOSTAT=ISTAT)  (YOBSINFILE(JJ),JJ=1,NOBSTYPE)
    IF ( ISTAT /= 0 ) CALL ABOR1_SFX("Error reading header in "//TRIM(YFILENAME))

    DO JI = 1,NOBSTYPE
      IF ( TRIM(COBS(JI)) /= TRIM(YOBSINFILE(JI))) THEN
        CALL ABOR1_SFX("Mapping of observations in "//TRIM(YFILENAME)//&
               " is not consistent with setup! "//TRIM(COBS(JI))//" /= "//TRIM(YOBSINFILE(JI)))
      ENDIF
    ENDDO
    DEALLOCATE(YOBSINFILE)
  ENDIF

  ! Do some reading
  WRITE(*,*) 'READING '//TRIM(HREC)//' AS INDEX ',IID,' IN '//TRIM(YFILENAME)
  ALLOCATE(ZWORK_TMP(INI))
  ALLOCATE(ZLINE(INVAR))
  DO JI = 1,INI
    ZLINE=XUNDEF
    READ (55,*,IOSTAT=ISTAT)  (ZLINE(JJ),JJ=1,INVAR)
    ZWORK_TMP(JI)=ZLINE(IID)
    IF ( ISTAT /= 0 ) CALL ABOR1_SFX("Error reading file "//TRIM(YFILENAME))
  ENDDO
  DEALLOCATE(ZLINE)
  CLOSE(55)

  ! For LOBSNAT we must convert back to FULL dimension
  IF ( LOBS .AND. LOBSNAT) THEN
    ZWORK_FULL(:)=XUNDEF
    DO JI=1,U%NDIM_NATURE
      ZWORK_FULL(U%NR_NATURE(JI))=ZWORK_TMP(JI)
    ENDDO
  ELSE
    ZWORK_FULL(:)=ZWORK_TMP(:)
  ENDIF

  DEALLOCATE(ZWORK_TMP)
ENDIF

! Distribute values on processors
IF (NPROC>1) THEN
  CALL READ_AND_SEND_MPI(ZWORK_FULL(:),ZWORK(:))
ELSE
  ZWORK(:)=ZWORK_FULL(:)
ENDIF
DEALLOCATE(ZWORK_FULL)

! Deal with masks
IF ( TRIM(HMASK) == "SEA" ) THEN
  IF ( U%NSIZE_SEA /= SIZE(PFIELD) ) CALL ABOR1_SFX("Mismatch in dimensions for sea mask and input field!")
  DO JI = 1,U%NSIZE_SEA
    PFIELD(JI)=ZWORK(U%NR_SEA(JI))
  ENDDO
ELSEIF ( TRIM(HMASK) == "NATURE" ) THEN
  IF ( U%NSIZE_NATURE /= SIZE(PFIELD) ) CALL ABOR1_SFX("Mismatch in dimensions for nature mask and input field!")
  DO JI = 1,U%NSIZE_NATURE
    PFIELD(JI)=ZWORK(U%NR_NATURE(JI))
  ENDDO
ELSEIF ( TRIM(HMASK) == "WATER" ) THEN
  IF ( U%NSIZE_WATER /= SIZE(PFIELD) ) CALL ABOR1_SFX("Mismatch in dimensions for water mask and input field!")
  DO JI = 1,U%NSIZE_WATER
    PFIELD(JI)=ZWORK(U%NR_WATER(JI))
  ENDDO
ELSEIF ( TRIM(HMASK) == "TOWN" ) THEN
  IF ( U%NSIZE_TOWN /= SIZE(PFIELD) ) CALL ABOR1_SFX("Mismatch in dimensions for town mask and input field!")
  DO JI = 1,U%NSIZE_TOWN
    PFIELD(JI)=ZWORK(U%NR_TOWN(JI))
  ENDDO
ELSEIF ( TRIM(HMASK) == "FULL" ) THEN
  PFIELD(:)=ZWORK(:)
ELSE
  CALL ABOR1_SFX("Mask "//TRIM(HMASK)//" not defined!")
ENDIF

DEALLOCATE(ZWORK)

IF (LHOOK) CALL DR_HOOK('ASSIM_READ_FIELD_ASCII',1,ZHOOK_HANDLE)

END SUBROUTINE ASSIM_READ_FIELD_ASCII

END MODULE MODE_ASSIM_READ_FIELD_ASCII

