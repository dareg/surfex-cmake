SUBROUTINE OL_PROPAGATE_ICE(KSIZE, KNUM_LAYERS, PLON, PLAT, PSIC_OLD, PSIC_NEW, PTICE, PZICE, PSDF, OMISSING_OLD_ICE, PICE_EDGE_THK)

USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32, REAL64

USE MODD_SURFEX_MPI, ONLY : NCOMM, NPROC, NRANK, LSFX_MPI
USE YOMHOOK,       ONLY: LHOOK, DR_HOOK, JPHOOK
USE PARKIND1,      ONLY: JPRB

USE MODE_SEAICE_SICE_PROPAGATE_ICE
USE MODI_ABOR1_SFX
IMPLICIT NONE

#ifdef SFX_MPI
INCLUDE 'mpif.h'
#endif
INTEGER, INTENT(IN) :: KSIZE
INTEGER, INTENT(IN) :: KNUM_LAYERS
REAL, DIMENSION (KSIZE), INTENT(IN) :: PLON
REAL, DIMENSION (KSIZE), INTENT(IN) :: PLAT
REAL, DIMENSION (KSIZE), INTENT(IN) :: PSIC_OLD
REAL, DIMENSION (KSIZE), INTENT(IN) :: PSIC_NEW
REAL, DIMENSION (KSIZE, KNUM_LAYERS), INTENT(IN OUT) :: PTICE
REAL, DIMENSION (KSIZE), INTENT(IN OUT) :: PZICE
REAL, DIMENSION (KSIZE), INTENT(OUT) :: PSDF
LOGICAL, INTENT(OUT) :: OMISSING_OLD_ICE
REAL, INTENT(OUT) :: PICE_EDGE_THK

INTEGER, ALLOCATABLE :: CHUNK_SIZES(:), CHUNK_DISPLS(:)
INTEGER, ALLOCATABLE :: PROC_MAP(:), PROC_MAP_TOT(:)
INTEGER :: SIZE_TOT, JI, JMYPROC, JNPROC
INTEGER :: INUM_BORDER_CELLS, INUM_BORDER_CELLS_SUM
INTEGER :: JERR

REAL, ALLOCATABLE :: ZLON_TOT(:), ZLAT_TOT(:)

REAL, ALLOCATABLE :: ZSIC_TOT_OLD(:), ZSIC_TOT_NEW(:), ZSDF_TOT_OLD(:)
REAL, ALLOCATABLE :: ZTICE_TOT_OLD(:,:), ZZICE_TOT_OLD(:)
LOGICAL :: GMISSING_OLD_ICE
INTEGER :: IMISSING_OLD_ICE
REAL :: ZICE_EDGE_THK, ZICE_EDGE_THK_SUM

INTEGER :: MPI_FLOAT_TYPE

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('OL_PROPAGATE_ICE', 0, ZHOOK_HANDLE)

#ifdef SFX_MPI
SELECT CASE(KIND(PLAT))
  CASE(REAL32)
    MPI_FLOAT_TYPE = MPI_REAL
  CASE(REAL64)
    MPI_FLOAT_TYPE = MPI_DOUBLE_PRECISION
  CASE DEFAULT
    CALL ABOR1_SFX('Unsupported floating point precision')
END SELECT
#endif

#ifdef SFX_MPI
  JMYPROC = NRANK
  JNPROC = NPROC
#else
  JMYPROC = 0
  JNPROC = 1
#endif

ALLOCATE(PROC_MAP(KSIZE))
PROC_MAP(:) = JMYPROC

ALLOCATE(CHUNK_SIZES(JNPROC), CHUNK_DISPLS(JNPROC))
#ifdef SFX_MPI
  CALL MPI_ALLGATHER(KSIZE, 1, MPI_INTEGER, CHUNK_SIZES, 1, MPI_INTEGER, NCOMM, JERR)
#else
  CHUNK_SIZES = KSIZE
#endif


SIZE_TOT = SUM(CHUNK_SIZES)
IF(JMYPROC == 0) THEN
  print*, '==+==', CHUNK_SIZES(:)
END IF

ALLOCATE(ZSIC_TOT_OLD(SIZE_TOT), ZSIC_TOT_NEW(SIZE_TOT))
ALLOCATE(ZLON_TOT(SIZE_TOT), ZLAT_TOT(SIZE_TOT), ZTICE_TOT_OLD(SIZE_TOT,KNUM_LAYERS))
ALLOCATE(ZZICE_TOT_OLD(SIZE_TOT), ZSDF_TOT_OLD(SIZE_TOT))
ALLOCATE(PROC_MAP_TOT(SIZE_TOT))

CHUNK_DISPLS(:) = 0
DO JI = 2, SIZE(CHUNK_SIZES)
  CHUNK_DISPLS(JI) = CHUNK_DISPLS(JI-1) + CHUNK_SIZES(JI-1)
END DO

! Gather all required data
#ifdef SFX_MPI
  CALL MPI_ALLGATHERV(PROC_MAP, KSIZE, MPI_INTEGER, PROC_MAP_TOT, CHUNK_SIZES, CHUNK_DISPLS, MPI_INTEGER, NCOMM, JERR)

  CALL MPI_ALLGATHERV(PLON, KSIZE, MPI_FLOAT_TYPE, ZLON_TOT, CHUNK_SIZES, CHUNK_DISPLS, MPI_FLOAT_TYPE, NCOMM, JERR)
  CALL MPI_ALLGATHERV(PLAT, KSIZE, MPI_FLOAT_TYPE, ZLAT_TOT, CHUNK_SIZES, CHUNK_DISPLS, MPI_FLOAT_TYPE, NCOMM, JERR)

  CALL MPI_ALLGATHERV(&
    PSIC_OLD, KSIZE, MPI_FLOAT_TYPE, &
    ZSIC_TOT_OLD, CHUNK_SIZES, CHUNK_DISPLS, MPI_FLOAT_TYPE, NCOMM, JERR)
  CALL MPI_ALLGATHERV(&
    PSIC_NEW, KSIZE, MPI_FLOAT_TYPE, &
    ZSIC_TOT_NEW, CHUNK_SIZES, CHUNK_DISPLS, MPI_FLOAT_TYPE, NCOMM, JERR)
  DO JI=1, KNUM_LAYERS
    CALL MPI_ALLGATHERV(     &
        PTICE(:,JI)          &
      , KSIZE                &
      , MPI_FLOAT_TYPE       &
      , ZTICE_TOT_OLD(:,JI)  &
      , CHUNK_SIZES          &
      , CHUNK_DISPLS         &
      , MPI_FLOAT_TYPE       &
      , NCOMM                &
      , JERR                 )
  END DO
  CALL MPI_ALLGATHERV(PZICE, KSIZE, MPI_FLOAT_TYPE, ZZICE_TOT_OLD, CHUNK_SIZES, CHUNK_DISPLS, MPI_FLOAT_TYPE, NCOMM, JERR)
#else
  PROC_MAP_TOT = PROC_MAP
  ZLON_TOT = PLON
  ZLAT_TOT = PLAT
  ZSIC_TOT_OLD = PSIC_OLD
  ZSIC_TOT_NEW = PSIC_NEW
  ZTICE_TOT_OLD = PTICE
  ZZICE_TOT_OLD = PZICE
#endif


CALL SEAICE_SICE_PROPAGATE_ICE( &
    SIZE_TOT                    &
  , PROC_MAP_TOT == JMYPROC     &
  , KSIZE                       &
  , KNUM_LAYERS                 &
  , ZLON_TOT                    &
  , ZLAT_TOT                    &
  , ZSIC_TOT_OLD                &
  , ZSIC_TOT_NEW                &
  , ZTICE_TOT_OLD               &
  , ZZICE_TOT_OLD               &
  , PTICE                       &
  , PZICE                       &
  , PSDF                        &
  , OMISSING_OLD_ICE            )

#ifdef SFX_MPI
! Exchange the extrapolated fields and smooth them to hide extrapolation
! artifacts from the final output.
  CALL MPI_ALLGATHERV(     &
      PSDF                 &
    , KSIZE                &
    , MPI_FLOAT_TYPE       &
    , ZSDF_TOT_OLD         &
    , CHUNK_SIZES          &
    , CHUNK_DISPLS         &
    , MPI_FLOAT_TYPE       &
    , NCOMM                &
    , JERR                 )

  CALL MPI_ALLGATHERV(     &
      PZICE                &
    , KSIZE                &
    , MPI_FLOAT_TYPE       &
    , ZZICE_TOT_OLD        &
    , CHUNK_SIZES          &
    , CHUNK_DISPLS         &
    , MPI_FLOAT_TYPE       &
    , NCOMM                &
    , JERR                 )

  DO JI=1, KNUM_LAYERS
    CALL MPI_ALLGATHERV(     &
        PTICE(:,JI)          &
      , KSIZE                &
      , MPI_FLOAT_TYPE       &
      , ZTICE_TOT_OLD(:,JI)  &
      , CHUNK_SIZES          &
      , CHUNK_DISPLS         &
      , MPI_FLOAT_TYPE       &
      , NCOMM                &
      , JERR                 )
  END DO
#else
  ZSDF_TOT_OLD = PSDF
  ZZICE_TOT_OLD = PZICE
  ZTICE_TOT_OLD = PTICE
#endif

CALL SEAICE_SICE_SMOOTHE_ICE(   &
    SIZE_TOT                    &
  , PROC_MAP_TOT == JMYPROC     &
  , KSIZE                       &
  , KNUM_LAYERS                 &
  , ZLON_TOT                    &
  , ZLAT_TOT                    &
  , ZSIC_TOT_OLD                &
  , ZSIC_TOT_NEW                &
  , ZTICE_TOT_OLD               &
  , ZZICE_TOT_OLD               &
  , ZSDF_TOT_OLD                &
  , PTICE                       &
  , PZICE                       &
  , PSDF                        )

! Get mean ice thickness along the ice edge
IF(OMISSING_OLD_ICE) THEN
  PICE_EDGE_THK = 0.
ELSE
  ZICE_EDGE_THK = 0.
  INUM_BORDER_CELLS = 0
  DO JI = 1, KSIZE
    IF(PSDF(JI) < -1.E+3 .AND. PSDF(JI) > -5.E+3) THEN
      ZICE_EDGE_THK = ZICE_EDGE_THK + PZICE(JI)
      INUM_BORDER_CELLS = INUM_BORDER_CELLS + 1
    END IF
  END DO

#ifdef SFX_MPI
    CALL MPI_ALLREDUCE(ZICE_EDGE_THK, ZICE_EDGE_THK_SUM, 1, MPI_FLOAT_TYPE, MPI_SUM, NCOMM, JERR)
    CALL MPI_ALLREDUCE(INUM_BORDER_CELLS, INUM_BORDER_CELLS_SUM, 1, MPI_INTEGER, MPI_SUM, NCOMM, JERR)
#else
    ZICE_EDGE_THK_SUM = ZICE_EDGE_THK
    INUM_BORDER_CELLS_SUM = INUM_BORDER_CELLS
#endif

  IF(INUM_BORDER_CELLS_SUM > 0) THEN
    PICE_EDGE_THK = ZICE_EDGE_THK_SUM/REAL(INUM_BORDER_CELLS_SUM, KIND=JPRB)
  ELSE
    PICE_EDGE_THK = MINVAL(PACK(ZZICE_TOT_OLD, ZSIC_TOT_OLD > 0.01))
    print*, 'All sea grid cells are covered by ice'
  END IF
END IF
print*, 'Mean ice thickness along the edge: ', PICE_EDGE_THK

DEALLOCATE(CHUNK_SIZES, ZSIC_TOT_OLD, ZSIC_TOT_NEW, ZLON_TOT, ZLAT_TOT)
DEALLOCATE(ZTICE_TOT_OLD, ZZICE_TOT_OLD)
DEALLOCATE(CHUNK_DISPLS)

IF (LHOOK) CALL DR_HOOK('OL_PROPAGATE_ICE', 1, ZHOOK_HANDLE)
END SUBROUTINE OL_PROPAGATE_ICE
