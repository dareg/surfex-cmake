MODULE MODE_SEAICE_SICE_PROPAGATE_ICE
USE NN_EXTRAPOLATE, ONLY: NEAREST_NEIGHBOUR_QUERY, DISTANCE_QUERY

USE YOMHOOK, ONLY: LHOOK, DR_HOOK, JPHOOK
IMPLICIT NONE

PRIVATE

REAL, PARAMETER :: SMOOTHE_RADIUS = 3.E+4
INTEGER, PARAMETER :: QNUM = 50
REAL, PARAMETER :: SMOOTHING_PAR = 8.E-5
REAL, PARAMETER :: EXTRAPOL_CUTOFF = -1.E+5

PUBLIC :: SEAICE_SICE_PROPAGATE_ICE, &
          SEAICE_SICE_SMOOTHE_ICE

CONTAINS

SUBROUTINE SEAICE_SICE_PROPAGATE_ICE(KSIZE_TOT, OMYPROC_TOT, KSIZE, KNUM_LAYERS, &
  PLON_TOT, PLAT_TOT, PSIC_TOT_OLD, PSIC_TOT_NEW, PTICE_TOT, PZICE_TOT, &
  PTICE_NEW, PZICE_NEW, PSDF, OMISSING_OLD_ICE)
IMPLICIT NONE
  INTEGER, INTENT(IN) :: KSIZE_TOT
  INTEGER, INTENT(IN) :: KSIZE
  INTEGER, INTENT(IN) :: KNUM_LAYERS
  ! Full-grid fields
  LOGICAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: OMYPROC_TOT ! .TRUE. -- current proc
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PLON_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PLAT_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PSIC_TOT_OLD
  REAL, DIMENSION (KSIZE_TOT, KNUM_LAYERS), INTENT(IN) :: PTICE_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PZICE_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PSIC_TOT_NEW
  ! Current proc only fields
  REAL, DIMENSION (KSIZE, KNUM_LAYERS), INTENT(IN OUT) :: PTICE_NEW
  REAL, DIMENSION (KSIZE), INTENT(IN OUT) :: PZICE_NEW
  REAL, DIMENSION (KSIZE), INTENT(OUT) :: PSDF
  LOGICAL, INTENT(OUT) :: OMISSING_OLD_ICE

  REAL, ALLOCATABLE :: ZSDF(:)
  REAL, ALLOCATABLE :: ZSDF_TOT(:)

  REAL, ALLOCATABLE :: NN_DIST(:), NN_DIST_INSIDE_ICE(:)

  REAL, ALLOCATABLE :: ZOPEN_SEA_LON(:), ZOPEN_SEA_LAT(:)
  REAL, ALLOCATABLE :: ZSEA_ICE_LON(:), ZSEA_ICE_LAT(:)

  INTEGER :: JI, JJ, JIOPEN_SEA, JISEA_ICE
  INTEGER :: IOUTSIDE_NEW_ICE_SIZE, IOPEN_SEA_SIZE, ISEA_ICE_SIZE
  INTEGER, ALLOCATABLE :: IOPEN_SEA_MASK(:), ISEA_ICE_MASK(:)
  INTEGER, ALLOCATABLE :: NN_INDEX(:)

  LOGICAL :: GMISSING_ICE
  REAL :: ZICE_EDGE_THK

  REAL, DIMENSION(KSIZE) :: ZLON_MYPROC, ZLAT_MYPROC, ZSIC_MYPROC_OLD, ZSIC_MYPROC_NEW, ZZICE_MYPROC

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  IF (LHOOK) CALL DR_HOOK('SEAICE_SICE_PROPAGATE_ICE', 0, ZHOOK_HANDLE)

  ZLON_MYPROC     = PACK(PLON_TOT,     OMYPROC_TOT)
  ZLAT_MYPROC     = PACK(PLAT_TOT,     OMYPROC_TOT)
  ZSIC_MYPROC_OLD = PACK(PSIC_TOT_OLD, OMYPROC_TOT)
  ZSIC_MYPROC_NEW = PACK(PSIC_TOT_NEW, OMYPROC_TOT)
  ZZICE_MYPROC    = PACK(PZICE_TOT,    OMYPROC_TOT)

  ALLOCATE(ZSDF(KSIZE))

  IF(COUNT(PSIC_TOT_OLD > 0) == 0) THEN
    GMISSING_ICE = .TRUE.
  ELSE
    GMISSING_ICE = .FALSE.
  END IF

  IF(.NOT. GMISSING_ICE) THEN
  print*, minval(PSIC_TOT_OLD), maxval(PSIC_TOT_OLD)
  print*, minval(ZSIC_MYPROC_NEW), maxval(ZSIC_MYPROC_NEW)

    IOPEN_SEA_SIZE = COUNT(OMYPROC_TOT .AND. .NOT. PSIC_TOT_OLD > 0.01)
    ISEA_ICE_SIZE  = COUNT(OMYPROC_TOT .AND.       PSIC_TOT_OLD > 0.01)

    print*, 'No of grid points with ice in previous/new ice field:', &
       ISEA_ICE_SIZE, '/', COUNT(ZSIC_MYPROC_NEW > 0.01)

    ALLOCATE(ZOPEN_SEA_LON(IOPEN_SEA_SIZE), &
             ZOPEN_SEA_LAT(IOPEN_SEA_SIZE), &
             NN_INDEX     (IOPEN_SEA_SIZE), &
             NN_DIST      (IOPEN_SEA_SIZE), &
             IOPEN_SEA_MASK(IOPEN_SEA_SIZE)  )

    ALLOCATE(ZSEA_ICE_LON(ISEA_ICE_SIZE), &
             ZSEA_ICE_LAT(ISEA_ICE_SIZE), &
             NN_DIST_INSIDE_ICE(ISEA_ICE_SIZE), &
             ISEA_ICE_MASK(ISEA_ICE_SIZE)  )

    ALLOCATE(ZSDF_TOT(KSIZE_TOT))

    JIOPEN_SEA = 1
    JISEA_ICE = 1

    DO JI = 1, KSIZE
      IF(.NOT. ZSIC_MYPROC_OLD(JI) > 0.01) THEN
        IOPEN_SEA_MASK(JIOPEN_SEA) = JI
        ZOPEN_SEA_LON(JIOPEN_SEA) = ZLON_MYPROC(JI)
        ZOPEN_SEA_LAT(JIOPEN_SEA) = ZLAT_MYPROC(JI)

        JIOPEN_SEA = JIOPEN_SEA + 1
      ELSE
        ISEA_ICE_MASK(JISEA_ICE) = JI
        ZSEA_ICE_LON(JISEA_ICE) = ZLON_MYPROC(JI)
        ZSEA_ICE_LAT(JISEA_ICE) = ZLAT_MYPROC(JI)

        JISEA_ICE = JISEA_ICE + 1
      END IF
    END DO

    ! Construct signed distance field
    CALL NEAREST_NEIGHBOUR_QUERY(PLON_TOT, PLAT_TOT, ZOPEN_SEA_LON, ZOPEN_SEA_LAT, &
      O_INPUT_MASK = PSIC_TOT_OLD > 0.01, K_INDEX = NN_INDEX, P_DIST = NN_DIST)

    ZSDF = 0.
    ZSDF(IOPEN_SEA_MASK) = NN_DIST

    ZSDF_TOT = 0.
    ZSDF_TOT = UNPACK(ZSDF, OMYPROC_TOT, ZSDF_TOT)

    CALL NEAREST_NEIGHBOUR_QUERY(PLON_TOT, PLAT_TOT, ZSEA_ICE_LON, ZSEA_ICE_LAT, &
      O_INPUT_MASK = .NOT. PSIC_TOT_OLD > 0.01 .AND. ZSDF_TOT < ABS(EXTRAPOL_CUTOFF), P_DIST = NN_DIST_INSIDE_ICE )

    ZSDF(ISEA_ICE_MASK) = - NN_DIST_INSIDE_ICE

    ZICE_EDGE_THK=1.0 ! Dummy value to avoid unexpected `holes' in the ice cover

    ! Extrapolate ice to the new border
    PTICE_NEW(IOPEN_SEA_MASK,:) = 271.3
    PZICE_NEW(IOPEN_SEA_MASK) = ZICE_EDGE_THK
    WHERE(ZSIC_MYPROC_NEW(IOPEN_SEA_MASK) > 0.01)
      PZICE_NEW(IOPEN_SEA_MASK) = PZICE_TOT(NN_INDEX)
    END WHERE
    DO JJ = 1, KNUM_LAYERS
      WHERE(ZSIC_MYPROC_NEW(IOPEN_SEA_MASK) > 0.01)
        PTICE_NEW(IOPEN_SEA_MASK,JJ) = PTICE_TOT(NN_INDEX,JJ)
      END WHERE
    END DO


    DEALLOCATE(ZOPEN_SEA_LON, ZOPEN_SEA_LAT)
    DEALLOCATE(ZSEA_ICE_LON, ZSEA_ICE_LAT)
    DEALLOCATE(NN_DIST, NN_DIST_INSIDE_ICE)
    DEALLOCATE(ISEA_ICE_MASK)
    DEALLOCATE(NN_INDEX)
    DEALLOCATE(ZSDF_TOT)
  ELSE
    ZSDF = 0.0
  ENDIF

  PSDF = ZSDF

  IF(.NOT. GMISSING_ICE) THEN
    OMISSING_OLD_ICE = .FALSE.
  ELSE
    OMISSING_OLD_ICE = .TRUE.
  END IF

  DEALLOCATE(ZSDF)

  IF (LHOOK) CALL DR_HOOK('SEAICE_SICE_PROPAGATE_ICE', 1, ZHOOK_HANDLE)
END SUBROUTINE SEAICE_SICE_PROPAGATE_ICE

SUBROUTINE SEAICE_SICE_SMOOTHE_ICE(KSIZE_TOT, OMYPROC_TOT, KSIZE, KNUM_LAYERS, &
  PLON_TOT, PLAT_TOT, PSIC_TOT_OLD, PSIC_TOT_NEW, PTICE_TOT, PZICE_TOT, PSDF_TOT, &
  PTICE_NEW, PZICE_NEW, PSDF_NEW)
IMPLICIT NONE
  INTEGER, INTENT(IN) :: KSIZE_TOT
  INTEGER, INTENT(IN) :: KSIZE
  INTEGER, INTENT(IN) :: KNUM_LAYERS
  ! Full-grid fields
  LOGICAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: OMYPROC_TOT ! .TRUE. -- current proc
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PLON_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PLAT_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PSIC_TOT_OLD
  REAL, DIMENSION (KSIZE_TOT, KNUM_LAYERS), INTENT(IN) :: PTICE_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PZICE_TOT
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PSIC_TOT_NEW
  REAL, DIMENSION (KSIZE_TOT), INTENT(IN) :: PSDF_TOT
  ! Current proc only fields
  REAL, DIMENSION (KSIZE, KNUM_LAYERS), INTENT(IN OUT) :: PTICE_NEW
  REAL, DIMENSION (KSIZE), INTENT(IN OUT) :: PZICE_NEW
  REAL, DIMENSION (KSIZE), INTENT(IN OUT) :: PSDF_NEW

  REAL, DIMENSION(KSIZE) :: ZSIC_MYPROC_OLD, ZSIC_MYPROC_NEW
  REAL, DIMENSION(KSIZE) :: ZLON_MYPROC, ZLAT_MYPROC
  LOGICAL :: GHAS_NEW_ICE(KSIZE)
  INTEGER, ALLOCATABLE :: INEW_ICE_MASK(:)
  REAL, ALLOCATABLE :: ZNEW_ICE_LON(:), ZNEW_ICE_LAT(:)

  INTEGER :: JI, JJ, JINEW_ICE
  INTEGER :: INEW_ICE_SIZE, IOUTSIDE_NEW_ICE_SIZE

  REAL :: ZWEIGHTS(QNUM), ZWEIGHTED_TICE(QNUM, KNUM_LAYERS)
  LOGICAL :: GMASK(KSIZE_TOT)
  INTEGER :: INDICES(QNUM)
  REAL :: ZTOTAL_WEIGHT
  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  IF (LHOOK) CALL DR_HOOK('SEAICE_SICE_SMOOTHE_ICE', 0, ZHOOK_HANDLE)

  ZLON_MYPROC     = PACK(PLON_TOT,     OMYPROC_TOT)
  ZLAT_MYPROC     = PACK(PLAT_TOT,     OMYPROC_TOT)
  ZSIC_MYPROC_OLD = PACK(PSIC_TOT_OLD, OMYPROC_TOT)
  ZSIC_MYPROC_NEW = PACK(PSIC_TOT_NEW, OMYPROC_TOT)

  GHAS_NEW_ICE =  ZSIC_MYPROC_NEW > 0.01 .AND. .NOT. ZSIC_MYPROC_OLD > 0.01

  HAS_NEW_ICE: IF(ANY(GHAS_NEW_ICE)) THEN
    INEW_ICE_SIZE = COUNT(GHAS_NEW_ICE)
    IOUTSIDE_NEW_ICE_SIZE = COUNT((ZSIC_MYPROC_OLD > 0.01) .OR. .NOT. ZSIC_MYPROC_NEW > 0.01)

    ALLOCATE(INEW_ICE_MASK(INEW_ICE_SIZE), &
             ZNEW_ICE_LON(INEW_ICE_SIZE), &
             ZNEW_ICE_LAT(INEW_ICE_SIZE))

    JINEW_ICE = 1
    DO JI = 1, KSIZE
      IF( ZSIC_MYPROC_NEW(JI) > 0.01 .AND. .NOT. ZSIC_MYPROC_OLD(JI) > 0.01) THEN
        INEW_ICE_MASK(JINEW_ICE) = JI
        ZNEW_ICE_LON(JINEW_ICE) = ZLON_MYPROC(JI)
        ZNEW_ICE_LAT(JINEW_ICE) = ZLAT_MYPROC(JI)

        JINEW_ICE = JINEW_ICE + 1
      END IF
    END DO

    GMASK = PSDF_TOT > EXTRAPOL_CUTOFF .AND. PSIC_TOT_NEW > 0.01
    print*, 'smoothing', INEW_ICE_SIZE, 'points', count(GMASK), '/', KSIZE_TOT
    DO JI = 1, INEW_ICE_SIZE
      CALL SMOOTHE(PLON_TOT, PLAT_TOT, GMASK, ZNEW_ICE_LON(JI), ZNEW_ICE_LAT(JI), INDICES, ZWEIGHTS)
      ZTOTAL_WEIGHT = SUM(ZWEIGHTS)

      DO JJ = 1, KNUM_LAYERS
        ZWEIGHTED_TICE(:,JJ) = ZWEIGHTS(:)*PTICE_TOT(INDICES,JJ)
      END DO
      PTICE_NEW(INEW_ICE_MASK(JI),:) = SUM(ZWEIGHTED_TICE, DIM=1)/ZTOTAL_WEIGHT
      PZICE_NEW(INEW_ICE_MASK(JI)  ) = SUM(ZWEIGHTS(:)*PZICE_TOT(INDICES))/ZTOTAL_WEIGHT
      PSDF_NEW (INEW_ICE_MASK(JI)  ) = SUM(ZWEIGHTS(:)*PSDF_TOT (INDICES))/ZTOTAL_WEIGHT
    END DO
    CALL NEAREST_NEIGHBOUR_QUERY(PLON_TOT, PLAT_TOT, 0., 0., K_NUMN=0, O_CLEANUP=.TRUE.)

    DEALLOCATE(ZNEW_ICE_LON, ZNEW_ICE_LAT)
    DEALLOCATE(INEW_ICE_MASK)
  END IF HAS_NEW_ICE
  IF (LHOOK) CALL DR_HOOK('SEAICE_SICE_SMOOTHE_ICE', 1, ZHOOK_HANDLE)

CONTAINS
SUBROUTINE SMOOTHE(PLON_TOT, PLAT_TOT, OMASK, PLON, PLAT, KINDICES, PWEIGHTS)
USE NN_EXTRAPOLATE, ONLY: NEAREST_NEIGHBOUR_QUERY
IMPLICIT NONE
  REAL,    INTENT(IN)                            :: PLON_TOT(:)
  REAL,    INTENT(IN), DIMENSION(SIZE(PLON_TOT)) :: PLAT_TOT
  LOGICAL, INTENT(IN), DIMENSION(SIZE(PLON_TOT)) :: OMASK
  REAL,    INTENT(IN)                            :: PLON, PLAT
  REAL,    INTENT(OUT)                           :: PWEIGHTS(:)
  INTEGER, INTENT(OUT), DIMENSION(SIZE(PWEIGHTS)) :: &
    KINDICES

  REAL :: ZDIST(SIZE(KINDICES))

  INTEGER :: JI, INN_COUNT
  REAL :: ZWEIGHT

  INN_COUNT = SIZE(KINDICES)

  CALL NEAREST_NEIGHBOUR_QUERY(PLON_TOT, PLAT_TOT, PLON, PLAT, &
    O_INPUT_MASK=OMASK, K_NUMN=INN_COUNT, K_INDEX=KINDICES, P_DIST=ZDIST)

  PWEIGHTS(:) = EXP(- ZDIST(:)*SMOOTHING_PAR)
  WHERE(KINDICES == 0)
    KINDICES = 1
    PWEIGHTS = 0.
  ENDWHERE
END SUBROUTINE SMOOTHE
END SUBROUTINE SEAICE_SICE_SMOOTHE_ICE

END MODULE MODE_SEAICE_SICE_PROPAGATE_ICE
