!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
!     #########
SUBROUTINE HORIZON_OROG(U,UG,USS,CSVF,LFSSOSVF)
!     #########################
!
!!*HORIZON_OROG  computes the horizon and SVF from orography
!!
!!
!!    METHOD
!!    ------
!!    See Senkova et al, 2007
!!   
!!    AUTHOR
!!    ------
!!
!!    A.Mary        Meteo-France
!!
!!    MODIFICATION
!!    ------------
!!
!!    Original    24/03/2015
!!    R.El Khatib 03/06/2021 Fix uninitialized ZMESH_SIZE.
!!    A.Napoly & H.Petithomme 2023 : optimisations
!!
!----------------------------------------------------------------------------
!
!*    0.     DECLARATION
!            -----------
!
USE MODD_SURF_ATM_GRID_n,ONLY: SURF_ATM_GRID_t
USE MODD_SURF_ATM_n,ONLY: SURF_ATM_t
USE MODD_SSO_n,ONLY: SSO_t
!
USE MODD_SURFEX_MPI,ONLY: NRANK,NPIO
USE MODD_CSTS,ONLY: XPI
USE MODD_SURF_PAR,ONLY: XUNDEF
USE MODD_PGDWORK,ONLY: XHALORADIUS
USE MODD_PGD_GRID,ONLY: CGRID
!
USE MODI_GET_MESH_DIM
USE MODI_GET_GRID_DIM
USE MODI_ABOR1_SFX
!
USE MODI_GATHER_AND_WRITE_MPI
USE MODI_READ_AND_SEND_MPI
!
USE PARKIND1,ONLY: JPRB
USE YOMHOOK,ONLY: LHOOK,DR_HOOK, JPHOOK
USE YOMLUN,ONLY: NULERR
!
IMPLICIT NONE
!
TYPE(SURF_ATM_T),INTENT(INOUT) :: U
TYPE(SURF_ATM_GRID_T),INTENT(IN) :: UG
TYPE(SSO_T),INTENT(INOUT) :: USS
CHARACTER(LEN=16),INTENT(IN) :: CSVF
LOGICAL,INTENT(IN) :: LFSSOSVF
!
INTEGER,DIMENSION(USS%NSECTORS) :: ILHAVG
INTEGER :: IIMAX,IJMAX,NGP,NGP2,NGPMAX,ISECT,JL,JI,JJ,JK,I,J,IIND,JF,IA,IB
REAL,DIMENSION(USS%NSECTORS,2) :: ZSECTORS
REAL,DIMENSION(USS%NSECTORS) :: ZLHAVG,ZLHMIN,ZLHMAX,ZHPHI
REAL :: ZRD,Z2PI,ZPI4,ZPI2,ZALPHA,ZCOSLAT,ZSINLAT,ZDLON,ZCOS,ZTAN,Y,ZTPHI,ZVPHI,ZSVF0,&
        ZDIST,ZM,ZTMP
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
REAL,DIMENSION(:),ALLOCATABLE :: ZDX,ZDY,ZMESH_SIZE,ZLON,ZLAT,ZZS,ZSLOPE,ZASPECT,ZSVF
REAL,DIMENSION(:,:),ALLOCATABLE :: ZLH,ZHMINS_DIR,ZHMAXS_DIR,ZFRAC_DIR,ZSLOPE_DIR
LOGICAL :: LLRECT

IF (LHOOK) CALL DR_HOOK("HORIZON_OROG",0,ZHOOK_HANDLE)

IF ( NRANK==NPIO ) THEN
   !
  ALLOCATE(ZLAT(U%NDIM_FULL),ZLON(U%NDIM_FULL))
  ALLOCATE(ZZS(U%NDIM_FULL))
  ALLOCATE(ZSLOPE(U%NDIM_FULL),ZASPECT(U%NDIM_FULL))
  ALLOCATE(ZFRAC_DIR (U%NDIM_FULL,USS%NSECTORS))
  ALLOCATE(ZSLOPE_DIR(U%NDIM_FULL,USS%NSECTORS))
  !
ELSE
  !
  ALLOCATE(ZLON(0),ZLAT(0),ZZS(0))
  ALLOCATE(ZSLOPE(0),ZASPECT(0))
  ALLOCATE(ZSLOPE_DIR(0,0),ZFRAC_DIR(0,0))
  !
ENDIF
!
CALL GATHER_AND_WRITE_MPI(UG%G%XLAT,ZLAT)
CALL GATHER_AND_WRITE_MPI(UG%G%XLON,ZLON)
CALL GATHER_AND_WRITE_MPI(USS%XAVG_ZS,ZZS)
CALL GATHER_AND_WRITE_MPI(USS%XSLOPE,ZSLOPE)
CALL GATHER_AND_WRITE_MPI(USS%XASPECT,ZASPECT)
CALL GATHER_AND_WRITE_MPI(USS%XSLOPE_DIR,ZSLOPE_DIR)
CALL GATHER_AND_WRITE_MPI(USS%XFRAC_DIR,ZFRAC_DIR)
!
!
IF ( NRANK==NPIO ) THEN
  ZRD = XPI/180
  Z2PI = 2*XPI
  ZLAT(:) = ZLAT(:)*ZRD
  ZLON(:) = ZLON(:)*ZRD
  !
  ALLOCATE(ZMESH_SIZE(U%NDIM_FULL))
  ALLOCATE(ZDX(U%NDIM_FULL),ZDY(U%NDIM_FULL))
  CALL GET_MESH_DIM(CGRID,UG%NGRID_FULL_PAR,U%NDIM_FULL,UG%XGRID_FULL_PAR,ZDX,ZDY,&
                    ZMESH_SIZE)
  DEALLOCATE(ZMESH_SIZE)
  !
  CALL GET_GRID_DIM(CGRID,UG%NGRID_FULL_PAR,UG%XGRID_FULL_PAR,LLRECT,IIMAX,IJMAX)
  !
  NGP = NINT(XHALORADIUS/(MINVAL(ZDX)+MINVAL(ZDY))*2)
  ! ngpmax = (2*ngp+1)**2-4*(2*ngp-1)
  ! outer points (ie points on perimeter) do not "enter the halo circle"
  NGPMAX = (2*NGP+1)**2-8*NGP
  IF (NGP == 0) CALL ABOR1("ERROR: HALO SIZE SMALLER THAN HALF MESH-SIZE")
  IF (NGP < 2) CALL ABOR1("ERROR: SINGLE POINT HALO")
  !
  ALLOCATE(ZLH(0:359,U%NDIM_FULL))
  !
  !$omp parallel do private(ngp,ngp2,i,j,ji,jj,ngpmax,jk,zm,zcoslat,zsinlat,zdist,zdlon,&
  !$omp ztmp,iind,isect,ia,ib)
  DO JL=1,U%NDIM_FULL
    ZM = (ZDX(JL)+ZDY(JL))/2
    NGP = NINT(XHALORADIUS/ZM)
    NGP2 = NGP**2
    I = MOD(JL-1,IIMAX)+1
    J = 1+(JL-I)/IIMAX
    ZCOSLAT = COS(ZLAT(JL))
    ZSINLAT = SIN(ZLAT(JL))
    ZLH(:,JL) = -HUGE(0.)
    ! rows 1-ngp and ngp-1 have no points, row 0 does not have extreme points (-ngp and ngp)
    DO JI=1-NGP,NGP-1
      IF (I+JI < 1.OR.I+JI > IIMAX) CYCLE
      ! ngpmax asserts jj**2+ji**2 < ngp**2
      NGPMAX = SQRT(REAL(NGP2-JI**2))
      IF (JI**2+NGPMAX**2 == NGP2) NGPMAX = NGPMAX-1
      DO JJ=-NGPMAX,NGPMAX
        IF (JI == 0.AND.JJ == 0.OR.J+JJ < 1.OR.J+JJ > IJMAX) CYCLE
        JK = I+JI+IIMAX*(J+JJ-1)
        IF (ZZS(JK) == XUNDEF) CYCLE
        ZDLON = ZLON(JL)-ZLON(JK)
        IIND = NINT(COMPASS(ZCOSLAT,ZSINLAT,ZDLON,ZLAT(JK))/ZRD)
        ZDIST = SQRT((JI*ZDX(JL))**2+(JJ*ZDY(JL))**2)
        ISECT = NINT(ZM/(ZDIST*ZRD))/2
        ZTMP = (ZZS(JK)-ZZS(JL))/ZDIST
        DO IA=IIND-ISECT,IIND+ISECT
          IF (IA < 0) THEN
            IB = IA+360
          ELSE IF (IA >= 360) THEN
            IB = IA-360
          ELSE
            IB = IA
          END IF
          ZLH(IB,JL) = MAX(ZLH(IB,JL),ZTMP)
        END DO
      END DO
    END DO
    WHERE(ZLH(:,JL) /= -HUGE(0.)) ZLH(:,JL) = SIN(ATAN(ZLH(:,JL)))
  END DO
  !$omp end parallel do
  !
  ALLOCATE(ZHMINS_DIR(U%NDIM_FULL,USS%NSECTORS))
  ALLOCATE(ZHMAXS_DIR(U%NDIM_FULL,USS%NSECTORS))
  ALLOCATE(ZSVF(U%NDIM_FULL))
  !
  ZPI2 = XPI/2
  ZPI4 = XPI/4
  !
  ZALPHA = Z2PI/USS%NSECTORS
  zsectors(1,1) = -zalpha/2
  DO JI=1,USS%NSECTORS
    ZSECTORS(JI,2) = ZSECTORS(1,1)+JI*ZALPHA
  END DO
  !
  ZSECTORS(2:USS%NSECTORS,1) = ZSECTORS(1:USS%NSECTORS-1,2)
  !
  DO JL=1,U%NDIM_FULL
    ZLHMIN(:) = 1.1
    ZLHMAX(:) = 0
    ZLHAVG(:) = 0
    ILHAVG(:) = 0
    DO JI=0,359
      IF (ZLH(JI,JL) == -HUGE(0.)) CYCLE
      ZTMP = MAX(ZLH(JI,JL),0.)
      ZALPHA = JI*ZRD
      IF (ZALPHA >= ZSECTORS(USS%NSECTORS,2)) ZALPHA = ZALPHA-Z2PI
      !
      DO JK=1,USS%NSECTORS
        IF (ZSECTORS(JK,1) <= ZALPHA.AND.ZALPHA < ZSECTORS(JK,2)) EXIT
      END DO
      !
      IF (JK <= USS%NSECTORS) THEN
        ILHAVG(JK) = ILHAVG(JK)+1
        ZLHAVG(JK) = ZLHAVG(JK)+ZTMP
        ZLHMIN(JK) = MIN(ZLHMIN(JK),ZTMP)
        ZLHMAX(JK) = MAX(ZLHMAX(JK),ZTMP)
      END IF
    END DO
    !
    WHERE (ILHAVG(:) /= 0) ZLHAVG(:) = ZLHAVG(:)/ILHAVG(:)
    WHERE (ZLHMIN(:) > 1) ZLHMIN(:) = 0
    !
    DO JK=1,USS%NSECTORS
      ZHMINS_DIR(JL,JK) = ZLHMIN(JK)
      ZHMAXS_DIR(JL,JK) = ZLHMAX(JK)
    END DO
    !
    IF (CSVF == "SENKOVA") THEN
      IF(LFSSOSVF) CALL ABOR1_SFX("Error: 'SENKOVA' and LFSSOSVF impossible")
      ZSVF(JL) = 1-SUM(ZLHAVG)/USS%NSECTORS
    ELSE IF (CSVF == "MANNERS") THEN
      ZHPHI(:) = ZPI2-ASIN(ZLHAVG(:))
      IF (LFSSOSVF.AND.SUM(ZFRAC_DIR(JL,:)) >= 0.99) THEN
        ZSVF(JL) = 0
        !
        DO JF=1,USS%NSECTORS
          ZSVF0 = 0
          ZTAN = TAN(ZSLOPE_DIR(JL,JF))
          !
          DO JK=1,USS%NSECTORS
            ZTMP = -ZTAN*COS(ZPI4*(JK-JF))
            ZTPHI = VPHI(ZTMP)
            ZVPHI = MIN(ZHPHI(JK),ZTPHI)+ZPI2-ZTPHI
            ZSVF0 = ZSVF0+SIN(ZVPHI)**2/USS%NSECTORS
          END DO
          !
          ZSVF(JL) = ZSVF(JL)+USS%XFRAC_DIR(JL,JF)*ZSVF0
        END DO
      ELSE
        ZSVF0 = 0
        ZTAN = TAN(ZSLOPE(JL))
        ! 
        DO JK=1,USS%NSECTORS
          ZTMP = -ZTAN*COS(ZASPECT(JL)-ZPI4*(JK-1))
          ZTPHI = VPHI(ZTMP)
          ZVPHI = MIN(ZHPHI(JK),ZTPHI)+ZPI2-ZTPHI
          ZSVF0 = ZSVF0+SIN(ZVPHI)**2/USS%NSECTORS
        END DO
        !
        ZSVF(JL) = ZSVF0
      END IF
    END IF
  END DO
  !
  DEALLOCATE(ZDX,ZDY,ZLH)
  !
ELSE
   ALLOCATE(ZHMINS_DIR(0,0),ZHMAXS_DIR(0,0))
   ALLOCATE(ZSVF(0))
ENDIF
! 
DEALLOCATE(ZLON,ZLAT,ZZS,ZSLOPE,ZSLOPE_DIR,ZFRAC_DIR,ZASPECT)
ZTMP = SUM(ZSVF)/SIZE(ZSVF)
CALL READ_AND_SEND_MPI(ZHMINS_DIR,USS%XHMINS_DIR)
CALL READ_AND_SEND_MPI(ZHMAXS_DIR,USS%XHMAXS_DIR)
CALL READ_AND_SEND_MPI(ZSVF,USS%XSVF)
DEALLOCATE(ZSVF,ZHMINS_DIR,ZHMAXS_DIR)
!
IF (LHOOK) CALL DR_HOOK("HORIZON_OROG",1,ZHOOK_HANDLE)
!
CONTAINS
  ELEMENTAL REAL FUNCTION COMPASS(ZCOSLAT,ZSINLAT,ZDLON,ZLAT)
    REAL,INTENT(IN) :: ZCOSLAT,ZSINLAT,ZDLON,ZLAT
    REAL :: ZCOS,ZALPHA
    ZCOS = COS(ZLAT)
    ZALPHA = Z2PI-ATAN2(SIN(ZDLON)*ZCOS,ZCOSLAT*SIN(ZLAT)-ZSINLAT*ZCOS*COS(ZDLON))
    COMPASS = MOD(ZALPHA,Z2PI)
  END FUNCTION

  REAL FUNCTION VPHI(Z)
    REAL,INTENT(IN) :: Z
    IF (ABS(Z) < 1.E-6) THEN
      VPHI = ZPI2
    ELSE
      VPHI = MOD(XPI+ATAN(1/Z),XPI)
    END IF
  END FUNCTION
END SUBROUTINE HORIZON_OROG
