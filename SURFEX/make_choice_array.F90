!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
SUBROUTINE MAKE_CHOICE_ARRAY(HPROGRAM, KNPATCH, ODIM, HRECFM, PWORK, HDIR, KPATCH, KRESP)
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK, JPHOOK
!
USE MODI_READ_SURF
USE MODI_ABOR1_SFX
!
IMPLICIT NONE
!
 CHARACTER(LEN=*), INTENT(IN) :: HPROGRAM
INTEGER, INTENT(IN) :: KNPATCH
LOGICAL, INTENT(IN) :: ODIM
 CHARACTER(LEN=*), INTENT(IN) :: HRECFM
REAL, DIMENSION(:,:), INTENT(INOUT) :: PWORK
!
 CHARACTER(LEN=*), INTENT(IN), OPTIONAL :: HDIR
INTEGER, INTENT(IN), OPTIONAL :: KPATCH
INTEGER, INTENT(OUT), OPTIONAL :: KRESP
!
 CHARACTER(LEN=12) :: YRECFM
INTEGER :: JP, IRESP, IPATCH
LOGICAL :: LFAIL
 CHARACTER(LEN=1) :: YDIR
 CHARACTER(LEN=2) :: YPAT
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('MAKE_CHOICE_ARRAY',0,ZHOOK_HANDLE)
!
YDIR = 'H'
IF (PRESENT(HDIR)) YDIR = HDIR
!
IPATCH = -1
IF (KNPATCH==1.AND.PRESENT(KPATCH)) IPATCH = KPATCH
!
LFAIL=.FALSE.
!
IF (ODIM) THEN
  DO JP=1,KNPATCH
    IF (IPATCH==-1) THEN
      WRITE(YPAT,'(I2)') JP
    ELSEIF (IPATCH/=0) THEN
      WRITE(YPAT,'(I2)') IPATCH
    ENDIF
    YRECFM=ADJUSTL(HRECFM(:LEN_TRIM(HRECFM)))
    IF (IPATCH/=0) YRECFM=TRIM(YRECFM)//'P'//ADJUSTL(YPAT(:LEN_TRIM(YPAT)))
    CALL READ_SURF(HPROGRAM,YRECFM,PWORK(:,JP),IRESP,HDIR=YDIR)
    LFAIL = ( LFAIL .OR. (IRESP /= 0) )
  ENDDO
ELSE
  CALL READ_SURF(HPROGRAM,HRECFM,PWORK(:,:),IRESP,HDIR=YDIR)
  LFAIL = ( IRESP /= 0 )
ENDIF
!
IF (PRESENT(KRESP)) THEN
 KRESP=IRESP
ELSE
 IF ( LFAIL ) THEN
  CALL ABOR1_SFX('ABORT: MAKE_CHOICE_ARRAY, IRESP IS NON-ZERO')
 ENDIF
ENDIF
!
IF (LHOOK) CALL DR_HOOK('MAKE_CHOICE_ARRAY',1,ZHOOK_HANDLE)
!
END SUBROUTINE MAKE_CHOICE_ARRAY

